#allow trader to set orders on your account
#this is the same as you send your api-key on common exchanges to the trader without sharing your private key or seed
#trader is allowed to set order on certain pairs
#order expiration time should be less then 2 minutes, as trader cannot cancel orders in current RIDE version
#at any time accout owner can cancel

let traderPublicKey = base58'QLMkJYFXsXiLLiKCbVCZZzQQT3whz8PAPdT7w4yQjEn'
let ownerPublicKey = base58'4gFmmsNv1qA9Krn3pUGbZVcJQTCsXEXhZjEtszDtxMXz'
let amountAsset = base58'8LLpj6yQLUu37KUt3rVo1S69j2gWMbgbM6qqgt2ac1Vb'
let matcherPublicKey = base58'7kPFrHDiGw1rCm7LPszuECwWYL3dMf6iMifLRDJQZMzy'


match (tx) {
    case o:Order =>
        let isWavesPriceAsset = !isDefined(o.assetPair.priceAsset)
        let rightPair = (o.assetPair.amountAsset == amountAsset) && isWavesPriceAsset
        sigVerify(o.bodyBytes, o.proofs[0], traderPublicKey)
        && rightPair
        && o.expiration - o.timestamp < 120*1000
        && o.matcherPublicKey == matcherPublicKey
    case t:SetScriptTransaction | TransferTransaction =>
        sigVerify(t.bodyBytes, t.proofs[0], ownerPublicKey)
    case _ =>
        false
 }